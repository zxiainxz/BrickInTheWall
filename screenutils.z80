; scadd - calculate the screen address
; inputs: h - x coordinate
;         l - y coordinate
; outputs: address in de
scadd:                  
                        ex af, af'

                        ld a, l
                        ; ld e, a                 ; store y in e

                        ; find line within cell

                        and 7                   ; lime 0-7 within char square
                        add a, 64               ; 64 * 256 = 16384 = start of screen display
                        ld d, a                 ; line * 256

                        ; find which third of the screen we're in

                        ld a, l                 ; restore y coord
                        and 192                 ; segment 0, 1 or 2 multiplied by 64
                        rrca                    ; divide this by 8
                        rrca
                        rrca                    ; segment 0-2 multiplied by 8
                        add a, d                ; add to d to give segment start address
                        ld d, a

                        ; find character cell within segment

                        ld a, l                 ; 8 char cells per segment
                        rlca                    ; divide y by 8 & multiply by 32
                        rlca                    ; net calculation: multiply by 4
                        and 224                 ; mask off bits we don't want
                        ld e, a                 ; vertical coordinate calculation done

                        ; add horizontal element

                        ld a, h
                        rrca                    ; only need to dovode by 8
                        rrca
                        rrca
                        and 31                  ; squares 0 - 31 across screen
                        add a, e                ; add to total so far
                        ld e, a                 ; de = screen address

                        ex af, af'
                        
                        ret

; NextRow2: get the next row
; Inputs:  de - current screen address
; Outputs: de - new screen address
; Trashes: af
NextRow2:                                                       ; Better than nextrow
                        inc d
                        ld a, d
                        and 0x07
                        ret nz

                        ld a, e
                        add a, 32
                        ld e, a
                        ret c

                        ld a, d
                        sub 8
                        ld d, a

                        ret

NextCharRow:
                        ld a, e
                        add a, 32
                        ld e, a

                        ret nc

                        add d, 8
                        ld d, a
                        
                        ret

; atadd - get the attribute address
; inputs: hl - x, y coordinates in character space
; outputs: de - attribute address
atadd:
                        push af                 ; protect af

                        ld a, l                 ; load y coord into a
                        rrca                    ; * 32
                        rrca
                        rrca
                        ld e, a                 ; store y * 32 into e
                        and 3                   ; mask the bits for moving into high byte
                        add a, 88               ; 88 * 256 = 22528 = ATTR_BASE
                        ld d, a                 ; move to d (*256)
                        ld a, e                 ; put y * 32 back into a
                        and 224                 ; mask low byte
                        ld e, a                 ; store into e
                        ld a, h
                        add a, e                ; add x coord to low byte
                        ld e, a                 ; store into e

                        pop af                  ; restore af
                        ret

; pixelattradd - get the attribute address of a given pixel
; inputs: hl - x, y coordinates in pixel space
; outputs: de - attribute address
pixelattradd:
                        push hl

                        srl h                    ; divide x and y by 8
                        srl h                    ; to get them into character space
                        srl h
                        srl h
                        sla h
                        srl l
                        srl l
                        srl l

                        call atadd

                        pop hl
                        ret


ClearScreen:
                        ld hl, $4000
                        ld de, $4001
                        ld bc, 6144
                        ld (hl), l
                        ldir

                        ld a, 56
                        ld bc, 767
                        ld (hl), a
                        ldir
                        ret

ClearAttrs:
                        ld hl, $5800
                        ld de, $5801
                        ld a, 56
                        ld bc, 767
                        ld (hl), a
                        ldir
                        ret

FadeScreenToWhite:
                        call ClearAttrs
                        ld bc, 0x0800
ClearCharRow:

                        ld a, 23
ClearNextCharRow:
                        push af
                        
                        add a
                        add a
                        add a

                        add b
                        sub 1

                        ld h, 0
                        ld l, a

                        call scadd
                        call ClearRow

                        pop af

                        dec a
                        cp 255
                        jr nz, ClearNextCharRow

                        halt
                        halt
                        halt      

                        inc c
                        djnz ClearCharRow

                        ret

ClearRow
                        push af

                        xor a

                        dup 32
                        ld (de), a
                        inc de
                        edup

                        pop af
                        ret

CopyBackBufferToScreen:
                        ld (CBBTS_SP+1), sp

                        ld hl, BACKBUFFER_BASE
                        exx
                        ld hl, SCREEN_BASE
CBBTS_NextLine:
                        exx

                        ld sp, hl

                        pop af, bc, de, ix

                        exx
                        ex af, af'

                        ld de, 16
                        add hl, de

                        pop af, bc, de, iy

                        ld (CBBTS_BUFFER1+1), sp

                        ld sp, hl
                        push iy, de, bc, af

                        ex af, af'
                        exx

                        push ix, de, bc, af

CBBTS_BUFFER1:
                        ld hl, 0
                        ld sp, hl

                        pop af, bc, de, ix
                        exx
                        ex af, af'

                        ld de, 16
                        add hl, de

                        pop af, bc, de, iy

                        ld (CBBTS_BUFFER2+1), sp
                        ld sp, hl

                        push iy, de, bc, af

                        ex af, af'
                        exx

                        push ix, de, bc, af

CBBTS_BUFFER2:
                        ld hl, 0
                        exx

CBBTS_SCREEN1:
                        ld a, h
                        cp 0x58
                        jr nz, CBBTS_NextLine
                        
CBBTS_SP:               ld sp, 0
                        exx
                        ret

; Draws the background to the back buffer, skipping the first char row
; Inputs: hl - background data, 16x16, linear
DrawBackground:
                        di

                        ld (DB_SP+1), sp            ; store stack
                        ld (DB_Reset+1), hl         ; store start of data
                        ld b, 16                    ; height of background, could be param
                        exx                         ; switch to shadow
                        ld hl, BACKBUFFER_BASE + 32 ; set backbuffer, skip first char row
                        ; ld hl, SCREEN_BASE + 32
DB_NextLine:
                        exx                         ; switch to standard

                        ld sp, hl                   ; load background to stack

                        pop ix                      ; take two bytes

                        inc hl                      ; skip over those two bytes
                        inc hl

                        dec b                       ; dec counter
                        jr nz, DB_NoReset

DB_Reset:               ld hl, 0                    ; reset hl to start of data
                        ld b, 16                    ; reset counter
DB_NoReset:

                        exx                         ; switch to shadow

                        ld (DB_Screen_HL+1), hl     ; store start of row

                        ld de, 32
                        add hl, de                  ; skip 32 bytes to the end of the row

                        ld sp, hl                   ; set stack to end of row

        .16             push ix                     ; push bg bytes across whole of the row

DB_Screen_HL:           ld hl, 0                    ; restore the start of the row

                        inc h                       ; find the next row
                        ld a, h
                        and 0x07
                        jr nz, DB_NextLine

                        ld a, h
                        sub 8
                        ld h, a
                        ld a, l
                        add a, 32
                        ld l, a
                        jr nc, DB_NextLine

                        ld a, h
                        add a, 8
                        ld h, a

                        cp BACKBUFFER_END_HIGH      ; stop if we're outside the buffer
                        ; cp SCREEN_END_HIGH
                        jp nz, DB_NextLine

DB_SP:                  ld sp, 0                    ; restore stack
                        exx                         ; back to normal registers

                        ei
                        
                        ret

; Inputs: hl - background
;          e - attr low byte
; Outputs: hl - background with offset
; y - presense of 32 bit
; x - presense of 1 bit
; 5800 - 00 - 0 -    00000000
; 5820 - 20 - 32 -   00100000
; 5840 - 40 - 64 -   01000000
; 5860 - 60 - 96 -   01100000
; 5880 - 80 - 128 -  10000000
; 58a0 - a0 - 160 -  10100000
;      - c0 - 192 -  11000000
;      - e0 - 224 -  11100000
;
;   01
;   23
;
; y * 2 + x
GetBGForCell:
                        ld a, e
                        and 1
                        bit 5, e
                        jr nz, GBGFC1
                        or 2
GBGFC1:
                        sla a
                        sla a
                        sla a
                        ld d, 0
                        ld e, a
                        add hl, de

                        ret
