; de - screen address
; a - ascii code
; output - ix: char sprite
GetChar:
                        cp '9'+1
                        jr c, NumberChar

                        sub 'A'
                        ld ix, fontCaps
                        jr CharDone

NumberChar:
                        sub '0'
                        ld ix, fontNumbers
CharDone:                        

                        add a
                        add a
                        add a

                        push de
                        ld d, 0
                        ld e, a

                        add ix, de
                        pop de

                        ret

; h, l: x, y
; bc: string address 0 terminated
DisplayString:
                        push hl
DisplayNextChar:
                        ld a, (bc)
                        cp 0
                        jr z, DisplayStringDone

                        cp 32
                        jr z, MoveToNextChar
                        call scadd
                        
                        push bc
                        call GetChar
                        call DrawSprite8x8
                        pop bc
                        
MoveToNextChar:
                        ld a, h
                        add 8
                        ld h, a
                        inc bc
                        jr DisplayNextChar

DisplayStringDone:
                        pop hl
                        ret

; h, l: x, y
; bc: string address 0 terminated
OverdrawString:
                        push hl, ix
OverdrawNextChar:
                        ld a, (bc)
                        cp 0
                        jr z, OverdrawStringDone

                        cp 32
                        jr z, OverdrawMoveToNextChar
                        call scadd
                        
                        push bc
                        call GetChar
                        call OverdrawSprite8x8NoShift
                        pop bc
                        
OverdrawMoveToNextChar:
                        ld a, h
                        add 8
                        ld h, a
                        inc bc
                        jr OverdrawNextChar

OverdrawStringDone:
                        pop ix, hl
                        ret

; bc: pointer to string
; returns x in h
CentreString:
                        call strlen

                        add a
                        add a

                        ld h, a

                        ld a, 128
                        sub h

                        ld h, a
                        ret
