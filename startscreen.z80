StartScreenInitialise:
                        ld a, 6
                        call ClearScreen
                        call InitialliseSpriteTable             ; Clear anything stored in the Sprite table by other scenes

                        ld de, 0x4085
                        call DrawBorder

                        ld hl, ATTR_BASE+234
                        ld a, 2
                        call DrawLogoAttrs
                        
                        ld a, 1
                        out (#fe), a

                        ld de, 0x46EB
                        ; ld de, 0x480B
                        call DrawLogo

                        ld hl, 0x3498                            ; 40, 96
                        ld bc, anykey_string
                        call DisplayString

                        jp InitialiseFinished

GetNextColour:
                        ld a, (introscenecolour)
                        inc a
                        ld (introscenecolour), a

                        cp 7
                        ret nz

                        ld a, 2
                        ld (introscenecolour), a
                        ret

StartScreenLoop:
                        halt

                        ld a, (secondcounter)
                        and a
                        jr nz, SSL_ReadKey

                        call GetNextColour
                        ld hl, ATTR_BASE+234
                        call DrawLogoAttrs

SSL_ReadKey:
                        ld bc, Row_SpaceSymMNB                  ; Check for Space
                        in a, (c)

                        and 1
                        jr z, StartGameKeys                             

                        ld bc, Row_09876
                        in a, (c)
                        and 1
                        jr z, StartGameCursor

                        ld bc, Row_54321
                        in a, (c)

                        bit 4, a
                        jr z, StartGameSinclair

                        ld bc, Joy_Kempston
                        in a, (c)
                        bit 4, a
                        jp nz, StartGameKempston

                        jr StartScreenLoop

StartGameKeys:
                        ld hl, HandleKeys
                        jr StartGame

StartGameSinclair:
                        ld hl, HandleSpectrum
                        jr StartGame

StartGameKempston:
                        ld hl, HandleKempston
                        jr StartGame

StartGameCursor:
                        ld hl, HandleCursor

StartGame:
                        ld (inputhandler), hl
                        call InitialiseScore

                        ld a, INTRO_SCENE
                        ld (currentloop), a                     ; Move to the game loop

                        jp SwitchMainLoop
